plugins {
    id 'net.minecraftforge.gradle'
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'org.spongepowered.mixin'
    id 'org.parchmentmc.librarian.forgegradle'
}

group = project.findProperty("mod_group") ?: 'com.chaosbuffalo'
archivesBaseName = "${mod_id}-${minecraft_version}"

minecraft {
    mappings channel: mappings_channel, version: mappings_version
}

java.toolchain.languageVersion = JavaLanguageVersion.of(getProperty("java_version") ?: 8);

repositories {
    mavenCentral()
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

def mixin_file = findProperty("mixin_file")

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${minecraft_version}-${forge_version}"
    implementation group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
    testAnnotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
}

//sourceSets.main.resources { srcDir 'src/generated/resources' }

def jarAttrs = [
        "Specification-Title": mod_id,
        "Specification-Vendor": mod_vendor,
        "Specification-Version": "1", // We are version 1 of ourselves
        "Implementation-Title": project.name,
        "Implementation-Version": jar.archiveVersion,
        "Implementation-Vendor": mod_vendor,
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]
if (mixin_file) {
    jarAttrs.put("MixinConfigs",  mixin_file)
}

jar {
    archiveBaseName = project.name
    manifest {
        attributes(jarAttrs)
    }
    from rootProject.file('LICENSE.txt')
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
